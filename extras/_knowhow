#compdef knowhow

() {

    local context state line
    local -a _commands _command_args
    typeset -A opt_args

    # commands will be shown in alphabetic order regardless of order here
    _commands=(
        "add:add a snippet to a knowhow repo"
        "dump:dump a knowhow repo to stdout"
        "help:show help about a knowhow command"
        "info:show info about a knowhow repo"
        "load:load a knowhow repo from stdin"
        "pprint:pretty-print a knowhow repo to stdout"
        "remove:remove a snippet from a knowhow repo"
        "search:search a knowhow repo"
        "tags:view tags used in a knowhow repo"
        "upgrade:upgrade the index of an existing knowhow repo"
    )

    if ! type knowhow &> /dev/null; then
        _message "can't find knowhow script: check PATH"
        return 1
    fi

    _arguments '*:: :-> command'

    if (( CURRENT == 1)); then
        _describe -t commands "mycommand" _commands
        return 0
    fi

    case "$words[1]" in
        search)
            _command_args=(
                '-t+:TAG[,TAG]*:'
                '--tag+:TAG[,TAG]*'
                '*:"search terms":'
            )
            ;;
        add)
            _command_args=(
                '-t+:TAG[,TAG]*'
                '--tag+:TAG[,TAG]*'
                '*:"snippet to add":'
            )
            ;;
        tags)
            _command_args=(
                '-c[include counts]'
                '--counts[include counts]'
                '-p+[limit to tags starting with PREFIX]:PREFIX:'
                '--prefix+[limit to tags starting with PREFIX]:PREFIX:'
                '*:"show tags"'
            )
            ;;
    esac

    _arguments ${_command_args} && return 0

    return 1
}

## sublimeconf: filetype=shell
